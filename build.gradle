plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version 'v0.0.2' apply false
	id 'net.minecrell.licenser' version '0.4.1'
	id "org.ajoberstar.grgit" version "3.1.1"
	id 'com.matthewprenger.cursegradle' version "1.4.0"
}

class Globals {
	static def baseVersion = "0.7.8"
	static def mcVersion = "1.16.3"
	static def yarnVersion = "+build.31"
	static def apiVersion = "0.23.0+build.410-1.16"
	static def changelog = ""
}

version = Globals.baseVersion

def getSubprojectVersion() {
	return Globals.baseVersion
}

allprojects {
	def projectName = it.name;
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	group = "fr.catcore"


	dependencies {
		if (projectName == "1.2.5" || projectName == "b1.7.3") {
			// fabric loader requires log4j, guava, and gson
			implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
			implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
			implementation 'com.google.guava:guava:28.0-jre'
			implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

			// mixin and fabric loader need asm 8
			implementation 'org.ow2.asm:asm:8.0'
			implementation 'org.ow2.asm:asm-analysis:8.0'
			implementation 'org.ow2.asm:asm-commons:8.0'
			implementation 'org.ow2.asm:asm-tree:8.0'
			implementation 'org.ow2.asm:asm-util:8.0'

			if (projectName == "1.2.5") {
				minecraft "com.mojang:minecraft:1.2.5"

				mappings loom.fromBranch("Blayyke/yarn", "1.2.5") { spec ->
					spec.version = "1.2.5-1"
				}
			} else {
				minecraft "com.mojang:minecraft:b1.7.3"

				mappings loom.fromCommit("minecraft-cursed-legacy/Plasma", "f900cd3") {spec ->
					spec.version = "b1.7.3-14"
				}

				modImplementation "com.github.minecraft-cursed-legacy:Cursed-Legacy-API:0.6.2"
			}
			modImplementation "com.github.minecraft-cursed-legacy:cursed-fabric-loader:8f014a3"
		} else if (projectName == "1.8.9") {
			compile "com.google.guava:guava:23.5-jre"
			minecraft "com.mojang:minecraft:1.8.9"
			mappings "net.fabricmc:yarn:1.8.9+build.202007011615:v2"
			modCompile ("net.fabricmc:fabric-loader-1.8.9:0.8.2+build.202004131640") {
				exclude module: "guava"
			}
		} else {
			if (projectName == "1.15.2") {
				minecraft "com.mojang:minecraft:1.15.2"
				mappings "net.fabricmc:yarn:1.15.2+build.17:v2"
				modImplementation "net.fabricmc.fabric-api:fabric-api:0.23.0+build.328-1.15"
			} else {
				minecraft "com.mojang:minecraft:$Globals.mcVersion"
				mappings "net.fabricmc:yarn:${Globals.mcVersion}${Globals.yarnVersion}:v2"
				modImplementation "net.fabricmc.fabric-api:fabric-api:${Globals.apiVersion}"
			}
			modCompile "net.fabricmc:fabric-loader:0.9.2+build.206"
		}
	}

	minecraft {
	}

	configurations {
		dev
	}

	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven {
			name = 'Fabric'
			url = 'http://maven.fabricmc.net/'
		}
		maven {
			name = 'SpongePowered'
			url = 'http://repo.spongepowered.org/maven'
		}
		maven {
			name = 'mojang'
			url = 'https://libraries.minecraft.net/'
		}
		maven {
			name = 'legacy-fabric'
			url = 'https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven'
		}
		maven {
			name = "Jitpack"
			url "https://jitpack.io/"
		}
	}

	jar {
		classifier = "dev"
	}

	afterEvaluate {
		remapJar {
			input = file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar")
			archiveName = "${archivesBaseName}-${version}.jar"
		}

		artifacts {
			dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: jar
		}

		processResources {
			inputs.property "version", project.version

			from(sourceSets.main.resources.srcDirs) {
				include "fabric.mod.json"
				expand "version": project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude "fabric.mod.json"
			}
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
}

subprojects {

	dependencies {
//        testmodCompile sourceSets.main.output
	}

	task remapMavenJar(type: Copy, dependsOn: remapJar) {
		afterEvaluate {
			from("${project.buildDir}/libs/$archivesBaseName-${version}.jar")
			into("${project.buildDir}/libs/")
			rename { String fn -> "$archivesBaseName-${version}-maven.jar" }
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				afterEvaluate {
					artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
						builtBy remapMavenJar
					}
					artifact(sourcesJar) {
						builtBy remapSourcesJar
					}
				}
			}
		}
	}
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
	afterEvaluate {
		input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
		archiveName = "${archivesBaseName}-${version}-maven.jar"
		addNestedDependencies = false
	}
}

subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }

dependencies {
	afterEvaluate {
		subprojects.each {
			compile project(path: ":${it.name}", configuration: "dev")
			include project("${it.name}:")
		}
	}
}

curseforge {
	apiKey = System.getenv('CURSEKEY') ?: "a key"
	project {
		id = "318315"
		releaseType = "release"
		changelog = Globals.changelog
		mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
			displayName = Globals.baseVersion
			relations {
				requiredDependency "fabric-api"
			}
		}

		gameVersionStrings.clear()
		gameVersionStrings.add("Fabric")

		// 1.16.x
		gameVersionStrings.add("1.16.3")
		gameVersionStrings.add("1.16.2")
		gameVersionStrings.add("1.16.1")
		gameVersionStrings.add("1.16")
		gameVersionStrings.add("1.16-Snapshot")

		// 1.15.x
		gameVersionStrings.add("1.15.2")
		gameVersionStrings.add("1.15.1")
		gameVersionStrings.add("1.15")
		gameVersionStrings.add("1.15-Snapshot")

		// 1.14.x
		gameVersionStrings.add("1.14.4")
		gameVersionStrings.add("1.14.3")
		gameVersionStrings.add("1.14.2")
		gameVersionStrings.add("1.14.1")
		gameVersionStrings.add("1.14")
		gameVersionStrings.add("1.14-Snapshot")

		// For beta 1.7
//		gameVersionStrings.add("1.0.0")
	}
	options{
		forgeGradleIntegration = false
	}
}